buildscript {
    repositories {
		// The plugin depends on the graphql-maven-plugin, whose snapshot versions are on the local maven repository.
		// So, for development reason, we need to access to the local maven repository. It's useless for standard use of the plugin
        mavenLocal()
        mavenCentral()
    }
    
    dependencies {
    	// The version number is not used in this multi-module project: the included build of graphql-gradle-plugin
    	// will replace this declaration. But this dummy declaration is needed!
    	//
    	// Of course, for a real use of the plugin in your project, you'll have to replace by the last plugin's version,
    	// or better: put this version number in the gradle.properties file, to use it for both the plugin and the dependencies version
        classpath 'com.graphql-java-generator:graphql-gradle-plugin:dummy'
    }
   
}

plugins {
	id 'java'
	id 'application'
	id 'com.graphql-java-generator.graphql-gradle-plugin'
	id 'org.springframework.boot' version "${spring3_springBootVersion}"
	id 'io.spring.dependency-management' version "${spring3_DependencyManagementPluginVersion}"
}
sourceCompatibility = '17'

repositories {
	// The plugin depends on the graphql-maven-plugin plugin logic module, which is in mavenCentral (for releases), and in mavenLocal 
	// when working on the plugin development.
	mavenLocal()		// Only if you work on the plugin development
	mavenCentral()
}

dependencies {
	// The graphql-java-client-dependencies module agregates all dependencies for the generated code
    implementation "com.graphql-java-generator:graphql-java-server-runtime:${project.pluginRuntimeVersion}" 

	// Then, the dependencies that are specific to this project
	implementation "org.springframework.boot:spring-boot-starter-websocket"  // Mandatory to activate the web sockets (mandatory for subscription)
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	
	runtimeOnly "com.h2database:h2"

	// Dependencies for tests
	testImplementation "org.springframework.boot:spring-boot-starter-test" 
}

// The line below adds the generated sources as a java source folder, in the IDE
sourceSets.main.java.srcDirs += '/build/generated/sources/graphqlGradlePlugin'
sourceSets.main.resources.srcDirs += '/build/generated/resources/graphqlGradlePlugin'

// Let's configure the GraphQL Gradle Plugin
// (the plugin will automatically add it as a dependency to compileJava and processResources) 
generateServerCodeConf {
	generateDataLoaderForLists = true
	generateJPAAnnotation = true
	javaTypeForIDType = "java.lang.Long"
	packageName = "org.forum.server.graphql"
	scanBasePackages = "org.forum.server.jpa , org.forum.server.specific_code, org.forum.server.oauth2"
	schemaPersonalizationFile = "src/main/graphql/forum_personalization.json"
	useJakartaEE9 = true
	customScalars = [ [
			graphQLTypeName: "Date",
			javaType: "java.util.Date",
			graphQLScalarTypeStaticField: "com.graphql_java_generator.customscalars.GraphQLScalarTypeDate.Date"
	] ]
}
